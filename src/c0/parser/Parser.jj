//JavaCCオプション
options {
	STATIC = false; //マルチスレッド環境で使えるようにするため、falseに
	DEBUG_PARSER = true; //デバッグ情報の出力
	UNICODE_INPUT = true; //日本語を処理する
	JDK_VERSION = "1.6";
}

PARSER_BEGIN(C0Language)
package c0.parser;

import c0.ast.*;
import c0.util.DataType;
import java.util.List;
import java.util.LinkedList;

public class C0Language {
	}
PARSER_END(C0Language)

//スキャナの記述

//空白類
SPECIAL_TOKEN: {
	<WHITESPACE: ([" ", "\f", "\n", "\r", "\t"])+>
}

//文のキーワード
TOKEN: {
	  <INT      : "int">
	| <IF       : "if">
	| <ELSE     : "else">
	| <WHILE    : "while">
	| <FOR      : "for">
	| <RETURN   : "return">
	| <BREAK    : "break">
}

//識別子
TOKEN: {
	<IDENTIFIER: ["A"-"Z", "a"-"z", "_"] (["A"-"Z", "a"-"z", "_", "0"-"9"])*>
}

//整数
TOKEN: {
	<INTEGER: ["1"-"9"] (["0"-"9"])*
			| "0"
	>
}

//一行コメント
//"//"、改行以外の文字（0回以上の繰り返し）、改行文字（省略可能）
SPECIAL_TOKEN: {
	<LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r\n" | "\r")?>
}

//文字列リテラル
MORE: { <"\""> : IN_STRING }
	<IN_STRING> MORE: {
		  <(~["\"", "\\", "\n", "\r"])+> //二重引用符'"', バックスラッシュ'\', 改行文字'\n', '\r'以外の1回以上の繰り返し
		| <"\\" ~[]> //エスケープコード
}
<IN_STRING> TOKEN: { <STRING: "\""> : DEFAULT }


//パーサの記述

//外部宣言 :=
//   {関数宣言}+
//	|外部変数宣言
AstNode file():
{
	Token token = null;
	List<IdentifierNode> functions = new LinkedList<IdentifierNode>();
	IdentifierNode function = null;
	List<DeclareVariableNode> globalVariables = null;
}
{
		//関数宣言と外部変数宣言の処理
		//構文エラーが発生した際はエラーを出力し、次の構文の処理を行う
		(
		  try {
			  function=defineFunction() { if(function != null) functions.add(function); } //ここの括弧に問題があった。あとで確認する
		  } catch(ParseException e) {
			//System.out.println(e.getMessage());
			e.printStackTrace();

				//文、ファイルの終わりが見つかるまでスキップする
				//この処理にバグがないかチェックする
				do {
					token = getNextToken();
				} while(token.image.equals(";") && token.image.equals("}") && token.kind != EOF);
		  }
		)*
		<EOF>
			{
				return new AstNode(globalVariables, functions);			}
	
}

//関数宣言 :=
//	データ型 単純宣言子 '(' [引数 {',' 引数}* ] ')' 複合文
IdentifierNode defineFunction():
{
	DataTypeNode dataType;
	ExpressionNode function;
	IdentifierNode function_i;
	List<ParameterNode> parameters = null;
	StatementNode block;
}
{
	dataType=type() function=primaryExpression() "(" [parameters=parameters()] ")" block=blockStatement() //角括弧も取り除く必要がある？
		{
			function_i = (IdentifierNode)function;
			function_i.setParameters(parameters);
			function_i.setBlock(block);
			return function_i;		}
}

//引数のリスト
List<ParameterNode> parameters():
{
	List<ParameterNode> parameters = new LinkedList<ParameterNode>();
	ParameterNode parameter1;
	ParameterNode parameter2;}
{
	parameter1=parameter() { if(parameter1 != null) parameters.add(parameter1); }
	(LOOKAHEAD(2) "," parameter2=parameter() { if(parameter2 != null) parameters.add(parameter2); } )*
	{
		return parameters;	}}

//引数 :=
//	データ型 識別子
ParameterNode parameter():
{
	Token token = null;
	DataTypeNode dataType;
	ExpressionNode parameterName;
	IdentifierNode parameterName_i;
}
{
	dataType=type() parameterName=primaryExpression()
		{
			parameterName_i = (IdentifierNode)parameterName;
			return new ParameterNode(new Location(token), dataType, parameterName_i);		}
}


//外部変数宣言 :=
//{変数宣言}+
List<DeclareVariableNode> declareGlobalVariable():
{
	List<DeclareVariableNode> declareVariables = new LinkedList<DeclareVariableNode>(); //すべての外部変数宣言
	DeclareVariableNode declareVar = null;
}
{
	(declareVar=declareVariable())* { if(declareVar != null) declareVariables.add(declareVar); }
	{
		return declareVariables;	}
}

//変数宣言 :=
//データ型 単純宣言子 ['=' 式] ';'
//|データ型 配列宣言子 ';'
DeclareVariableNode declareVariable():
{
	Token token = null;
	DataTypeNode dataType = null;
	ExpressionNode variable = null;
	IdentifierNode variable_i = null;
	ExpressionNode expression = null;
}
{
	//一つにするべき？
	dataType=type() variable=primaryExpression() ["=" expression=expression()] ";"
		{
			variable_i = (IdentifierNode)variable;
			return new DeclareVariableNode(new Location(token), dataType, variable_i, expression);		}
}

//型の構文
//データ型 :=
//	'int'
DataTypeNode type():
{
	DataType dataType; //基本データ型
	ExpressionNode elementNumber = null; //要素数
}
{
	<INT>
	{
		return new DataTypeNode(DataType.INT, elementNumber); //整数の変数
	}
}

//文 :=
//	式文
//	|複合文
//	|条件文
//	|繰り返し文
//	|return文
//	|空文
StatementNode statement():
{
	StatementNode statementNode;
}
{
	( statementNode=expressionStatement()
	| statementNode=blockStatement()
	| statementNode=conditionalStatement()
	| statementNode=iterationStatement()
	| statementNode=returnStatement()
	| statementNode=emptyStatement()
	)
		{
			return statementNode;		}
}

//複合文 :=
//	'{' 局所変数宣言 {文}* '}'
StatementNode blockStatement():
{
	Token token;
	List<DeclareVariableNode> localVariables; //ローカル変数の宣言
	List<StatementNode> statements; //ブロック文本体
}
{
	token="{" localVariables=declareLocalVariables() statements=statements() "}"
	{
		return new BlockNode(new Location(token), localVariables, statements);	}
}

//ブロック文本体
List<StatementNode> statements():
{
	List<StatementNode> statements = new LinkedList<StatementNode>(); //文のリスト
	StatementNode statement;
}
{
	(statement=statement())+ { if(statement != null) statements.add(statement); }
	{
		return statements;
	}
}

//局所変数宣言 :=
//	{変数宣言}+
List<DeclareVariableNode> declareLocalVariables():
{
	List<DeclareVariableNode> declareVariables = new LinkedList<DeclareVariableNode>(); //すべての局所変数宣言
	DeclareVariableNode declareVar = null;
}
{
	(declareVar=declareVariable())* { if(declareVar != null) declareVariables.add(declareVar); }
	{
		return declareVariables;	}
}

//条件文 :=
//	if文
//この箇所は削除するか検討する
StatementNode conditionalStatement():
{
	StatementNode conditionalStatementNode;
}
{
	conditionalStatementNode=ifStatement() { return conditionalStatementNode; }
}

//if文 :=
//	'if' '(' 式 ')' 文 ['else' 文]
StatementNode ifStatement():
{
	Token token;
	ExpressionNode conditional = null;
	StatementNode thenBody = null;
	StatementNode elseBody = null;
}
{
	token=<IF> "(" conditional=expression() ")" thenBody=statement() [LOOKAHEAD(1) <ELSE> elseBody=statement()]
	{ return new IfNode(new Location(token), conditional, thenBody, elseBody); }
}

//繰り返し文 :=
//	while文
//	|for文
StatementNode iterationStatement():
{
	StatementNode iterationStatement;
}
{
	  iterationStatement=whileStatement() { return iterationStatement; }
	| iterationStatement=forStatement() { return iterationStatement; }
}

//while文 :=
//	'while' '(' 式 ')' 文
StatementNode whileStatement():
{
	Token token;
	ExpressionNode conditional;
	StatementNode body;
}
{
	token=<WHILE> "(" conditional=expression() ")" body=statement() { return new WhileNode(new Location(token), conditional, body); }
}

//for文 :=
//	'for' '(' [式] ';' [式] ';' [式] ')' 文
//初期化部や更新部で複数の式文を実行出来るようするべきか
StatementNode forStatement():
{
	Token token;
	ExpressionNode initialize = null;
	ExpressionNode conditional = null;
	ExpressionNode update = null;
	StatementNode body = null;
}
{
	token=<FOR> "(" [initialize=expression()] ";" [conditional=expression()] ";" [update=expression()] ")" body=statement()
	{ return new ForNode(new Location(token), initialize, conditional, update, body); }
}

//return文 :=
//	'return' [式] ';'
StatementNode returnStatement():
{
	Token token;
	ExpressionNode expressionNode = null;
}
{
	token=<RETURN> [expressionNode=expression()] ";" { return new ReturnNode(new Location(token), expressionNode); }
}

//空文 :=
//	';'
//あとで見直す
StatementNode emptyStatement():
{
	Token token;
}
{
	token=";" { return new EmptyStatementNode(new Location(token)); }
}

//式文 :=
//	式 ';'
ExpressionStatementNode expressionStatement():
{
	Token token;
	ExpressionNode expressionNode;
}
{
	//要修正。式のコンストラクタとメソッドを追加してから書き換える
	expressionNode=expression() token=";" { return new ExpressionStatementNode(new Location(token), expressionNode); }
}

//式の構文
//演算子の優先順位が低い順に書く

//代入式
ExpressionNode expression():
{
	ExpressionNode right = null;
	ExpressionNode left = null;
	IdentifierNode leftValue = null;
	ExpressionNode expressionNode = null;
}
{
	  LOOKAHEAD(3) left=term() "=" right=expression()
		{
			leftValue = (IdentifierNode)left;
			return new AssignNode(leftValue, right);
		}
	| expression5()
		{
			return expressionNode;
		}
}

//論理OR
ExpressionNode expression5():
{
	ExpressionNode right;
	ExpressionNode left;
}
{
	left=expression4() ("||" right=expression4() { left = new LogicalOrNode(right, left); })*
							{
								return left;
							}
}

//論理AND
ExpressionNode expression4():
{
	ExpressionNode right;
	ExpressionNode left;
}
{
	left=expression3() ("&&" right=expression3() { left = new LogicalAndNode(right, left); })*
						   {
						   		return left;
						   }
}

//同等式
//関係式
ExpressionNode expression3():
{
	ExpressionNode right;
	ExpressionNode left;
}
{
	left=expression2() ( "==" right=expression2() { left = new EquivalenceNode(right, left); }
					   | "!=" right=expression2() { left = new NotEquivalenceNode(right, left); }
					   | "<"  right=expression2() { left = new LessThanNode(right, left); }
					   | ">"  right=expression2() { left = new GreaterThanNode(right, left); }
					   | "<=" right=expression2() { left = new LessThanOrEqualNode(right, left); }
					   | ">=" right=expression2() { left = new GreaterThanOrEqualNode(right, left); }
					   )*
							{
								return left;
							}
}

//加算式
//減算式
ExpressionNode expression2():
{
	ExpressionNode right;
	ExpressionNode left;
}
{	
	left=expression1()  ( "+" right=expression1() { left = new PlusNode(right, left); }
						| "-" right=expression1() { left = new MinusNode(right, left); }
						)*
							{
								return left;
			   				}
}

//乗算式
//除算式
ExpressionNode expression1():
{
	ExpressionNode right;
	ExpressionNode left;
}
{
	left=term() ( "*" right=term() { left = new MulNode(right, left); }
		   		| "/" right=term() { left = new DivNode(right, left); }
		   		)*
		   			{						return left;
		   			}
}

//項の構文
ExpressionNode term():
{
	ExpressionNode expressionNode;
}
{
	expressionNode=unaryExpression() { return expressionNode; }
}

//単項式
ExpressionNode unaryExpression():
{
	Token token;
	ExpressionNode expressionNode;
}
{
	  token="++" expressionNode=unaryExpression() { return new PreIncrementNode(expressionNode); } //前置増分式
	| token="--" expressionNode=unaryExpression() { return new PreDecrementNode(expressionNode); } //前置減分式
	| token="-" expressionNode=unaryExpression() { return new UnaryMinusNode(expressionNode); } //単項マイナス式
	| token="!" expressionNode=unaryExpression() { return new ExclamationNode(expressionNode); } //論理否定
	| expressionNode=PostfixExpression() { return expressionNode; } //後置式
}

//後置式
ExpressionNode PostfixExpression():
{
	Token token = null;
	ExpressionNode expressionNode = null;
	ExpressionNode index = null;
	List<ExpressionNode> arguments = null;
}
{
	expressionNode=primaryExpression()
	( token="++" { expressionNode = new PostIncrementNode(expressionNode); } //後置増分式
	| token="--" { expressionNode = new PostDecrementNode(expressionNode); } //後置減分式
	| token="[" index=expression() "]" { expressionNode = new ArraySubscriptExpressionNode(expressionNode, index); } //添字式
	| token="(" [arguments=arguments()] ")" { expressionNode = new CallNode(expressionNode, arguments); } //関数呼び出し
	)*
		{
			return expressionNode;		}
}

//引数
List<ExpressionNode> arguments():
{
	List<ExpressionNode> arguments = new LinkedList<ExpressionNode>();
	ExpressionNode argument;}
{
	argument=expression() { arguments.add(argument); }
	("," argument=expression() { arguments.add(argument); })*
		{
			return arguments;		}}

//一次式
ExpressionNode primaryExpression():
{
	Token token;
	ExpressionNode expressionNode;
}
{
	token=<INTEGER> //10進定数
			{
				return new LiteralNode(new Location(token), DataType.INT, token.image);
			}
	| token=<STRING> //文字列定数
			{
				return new LiteralNode(new Location(token), DataType.STRING, token.image);
			}
	| token=<IDENTIFIER> //識別子
			{
				return new IdentifierNode(new Location(token), token.image);
			}
	| "(" expressionNode=expression() ")" //括弧式
			{
				return expressionNode;			}
}